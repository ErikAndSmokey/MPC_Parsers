\ THIS PROGRAM IS FOR
\ PERSEVERANCE TRAINING
\ THE LEFT LEVER IS THE REWARDED LEVER
\Created by Erik Van Newenhizen 1_3_2022

\This is FR1

\This section is for Inputs

^LeftLeverPress = 1
^RightLeverPress = 2

\This section is for Outputs

^LeftLeverExtend = 1
^RightLeverExtend = 2
^LeftCueLight = 3
^RightCueLight = 4
^House = 7
^Reward = 6 \In this code, this is a dipper
^Pump = 5
^TTL1 = 17

\Variable Definitions 
\ A = LL presses from initiation
\ B = RL presses from initiation
\ C = Phase start time (in seconds from initiation)
\ D = TOTALS (time, Left lever, right lever, total presses)
\ E = Moving across arrays
\ F = Deciding what phase to be in (1 for 30 minute phase, 2 for 15 minute phase)
\ G = TOTAL phase counter
\ H = Pump timer (i.e., countdown until the pump is turned OFF)
\ T = Timer (seconds)
\ X = Fixed ratio (set @ s.s.1, s1)
\ Y = Phase 2, 4, 5 time limit (set @ s.s.1, s1)
\ W = Phase 1 and 3 time limit (set @ s.s.1, s1)


\WTF IS E EXACTLY?
\ E(0) = LL array mover
\ E(1) = RL array mover
\ E(2) = Phase array mover

\WTF IS D EXACTKLY?
\ D(0) = time (seconds) since session start
\ D(1) = left lever presses
\ D(2) = right lever presses
\ D(3) = total lever presses


\Dimming
DIM A = 1000
DIM B = 1000
DIM C = 6
DIM D = 4
DIM E = 3


\What do your Z-pulses mean?
\Z1 = RESET TIMER AND START CHECKING TO SEE IF REACHED PHASE LIMIT
\Z2 = Terminate at the current spot in the phase no matter what is happening
\Z3 = Inter-phase interval
\Z4 = Use this as a reset signal to start back at the top of everything
\Z5 = This is how we control infusions if we stop the phase in the middle of getting an infusion... gives full infusion then STOPS


\---------------------------------------------------------------------------------------------------
S.S.1, \The main loop of the program... also sets some of the major variables in S1

S1,
#START: SET Y = 900, W = 1800, X = 1, F = 1; ON ^Pump, ^LeftCueLight ---> S2

S2,
1.3": OFF ^Pump ---> S3

S3,
13.7": OFF ^LeftCueLight; ON ^House, ^LeftLeverExtend, ^RightLeverExtend; Z1 ---> S8

S4,
H#T: OFF ^Pump, ^LeftCueLight ---> S5

S5,
#Z4: IF F = 1 [@T, @F]
    @T: SET F = 2 ---> S11
    @F: IF G = 5 [@T2, @F2]
        @T2: SET F = 2; ON ^Pump, ^LeftCueLight ---> S6
        @F2: SET F = 1; ON ^Pump, ^LeftCueLight ---> S6

S6,
1.3": OFF ^Pump ---> S7

S7,
13.7": OFF ^LeftCueLight; ON ^House, ^LeftLeverExtend, ^RightLeverExtend; Z1 ---> S8

S8,
X#R^LeftLeverPress: Z5; ON ^Pump, ^LeftCueLight; OFF ^House ---> S9
#Z2: OFF ^RightLeverExtend, ^LeftLeverExtend, ^House ---> S5

S9,
1.3": OFF ^Pump ---> S10
#Z2: OFF ^RightLeverExtend, ^LeftLeverExtend, ^House ---> S4

S10,
13.7": OFF ^LeftCueLight; ON ^House---> S8
#Z2: OFF ^RightLeverExtend, ^LeftLeverExtend, ^House, ^LeftCueLight ---> S5


S11,
15": ON ^House, ^LeftLeverExtend, ^RightLeverExtend; Z1 ---> S12

S12,
#Z2: OFF ^House, ^LeftLeverExtend, ^RightLeverExtend ---> S5



\-------------------------------------------------------------------------------------------------
S.S.2, \Inter-phase timer

S1,
#Z1: SET T = 0, C(E(2)) = D(0); ADD E(2); SET C(E(2)) = -987.987  ---> S2

S2,
0.1": SET T = T+0.1; IF F = 1 [@T, @F]
    @T: IF T >= W [@T2, @F2]
        @T2: ADD G; Z2 ---> S3
        @F2: ---> S2
    @F: IF T >= Y [@T3, @F3]
        @T3: ADD G; Z2 ---> S3
        @F3: ---> S2

S3,
15": Z4 ---> S1


\---------------------------------------------------------------------------------------------------
S.S.3, \For tracking lever presses

S1,
#START: ---> S2

S2,
#R^LeftLeverPress: SET A(E(0)) = D(0); ADD D(1), D(3), E(0); SET A(E(0)) = -987.987 ---> S2
#R^RightLeverPress: SET B(E(1)) = D(0); ADD D(2), D(3), E(1); SET B(E(1)) = -987.987 ---> S2




\---------------------------------------------------------------------
S.S.4, \Session Timer

S1,
#START: SET D(0) = 0 ---> S2

S2,
0.1": SET D(0) = D(0)+0.1; SHOW 1, Seconds, D(0) ---> S2



\-----------------------------------------------------------------------
S.S.5, \Counts how many phases we're in... also criteria for shutting off the program

S1,
#START: SET G = 1 ---> S2

S2,
0.1": IF G = 6 [@T, @F]
    @T: OFF ^House, ^LeftLeverExtend, ^RightLeverExtend, ^LeftCueLight, ^Pump ---> STOPABORTFLUSH
    @F: ---> S2

\---------------------------------------------------------------------
S.S.6, \Where we show all of the bits

S1,
#START: SHOW 2, Total Presses, D(3); SHOW 3, LL Presses, D(1); SHOW 4, RL Presses, D(2); SHOW 5, Phase, G ---> S2

S2,
0.1": SHOW 2, Total Presses, D(3); SHOW 3, LL Presses, D(1); SHOW 4, RL Presses, D(2); SHOW 5, Phase, G ---> S2



\---------------------------------------------------------------------
S.S.7, \This helps track how long it has been since the infusion pump was turned on

S1,
#Z5: SET H = 1.3"; SHOW 6, PUMP TIMER, H ---> S2

S2,
0.1": SET H = H - 0.1"; IF H <= 0 [@T, @F]
    @T:  SHOW 6, PUMP TIMER, H---> S1
    @F:  SHOW 6, PUMP TIMER, H---> S2